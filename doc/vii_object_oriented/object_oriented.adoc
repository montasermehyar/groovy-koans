[[object-oriented]]

== Object Oriented
<<<

=== Defining classes and scripts

==== Defining fields and local variables

===== DECLARING VARIABLES

Groovy uses Java’s modifiers—the keywords private , protected , and public for
modifying visibility 2 ; final for disallowing reassignment; and static to denote class
variables. A nonstatic field is also known as an instance variable. These modifiers all have
the same meaning as in Java.

The default visibility for fields has a special meaning in Groovy. When no visibility
modifier is attached to a field declaration, a property is generated for the respective
name.

Defining the type of a variable is optional. But the identifier must not stand alone
in the declaration. When no type and modifier are given, the def keyword must be
used as a replacement, effectively indicating that the field or variable can be assigned
an object of any type at runtime.

[source,groovy]
----
class ClassWithTypedAndUntypedFieldsAndProperties {
    public fieldWithModifier
    String typedField
    def untypedField
    protected field1, field2, field3
    private assignedField = new Date ( )
    static classField
    public static final String CONSTA = 'a', CONSTB = 'b'

    def someMethod() {
        def localUntypedMethodVar = 1
        int localTypedMethodVar = 1
        def localVarWithoutAssignment, andAnotherOne
    }
}

def localvar = 1
boundvar1 = 1

def someMethod() {
    def localMethodVar = 1
    boundvar2 = 1
}

someMethod()
----

NOTE: Java’s default package-wide visibility is supported via the `@PackageScope` annotation.

Assignments to typed references must conform to the type.

Groovy provides autoboxing and coercion when it makes sense. All other cases are
type-breaking assignments and lead to a ClassCastException at runtime,

[source,groovy]
----
final String PI = '3.14'
assert PI.class.name == 'java.lang.String'
assert PI.size() == 4
GroovyAssert.shouldFail(ClassCastException){
    Float areaOfCircleRadiusOne = PI
}
----

==== REFERENCING AND DEREFERENCING FIELDS

In addition to referring to fields by name with the obj.fieldname syntax, they can
also be referenced with the subscript operator. This allows
you to access fields using a dynamically determined name.

.Referencing fields with the subscript operator
****
[source,groovy]
----
class Counter {
    public count = 0
}

def counter = new Counter()
counter.count = 1
assert counter.count == 1
def fieldName = 'count'
counter[fieldName] = 2
assert counter['count'] == 2
----
****

**Can I override the subscript operator? **

Sure you can, and you’ll extend but
not override the general field-access mechanism that way. But you can do even better
and extend the field-access operator!

Extending the general field-access mechanism
[source,groovy]
----
 class PretendFieldCounter {
     public count = 0

     Object get(String name) {
         return 'pretend value'
     }

     void set(String name, Object value) {
         count++
     }
 }

 def pretender = new PretendFieldCounter()
 assert pretender.isNoField == 'pretend value'
 assert pretender.count == 0
 pretender.isNoFieldEither = 'just to increase counter'
 assert pretender.count == 1
----

==== Methods and parameters

[source,groovy]
----
class ClassWithTypedAndUntypedMethods {
    static void main(args) {
        def some = new ClassWithTypedAndUntypedMethods()
        some.publicVoidMethod()
        assert 'hi' == some.publicUntypedMethod()
        assert 'ho' == some.publicTypedMethod()
        combinedMethod()
    }

    void publicVoidMethod() {}

    def publicUntypedMethod() {
        return 'hi'
    }

    String publicTypedMethod() {
        return 'ho'
    }

    private static final void combinedMethod() {}
}
----

[source,groovy]
----
//Java
public static void main (String[] args)
//groovy
static main (args)
----
NOTE: The Java compiler fails on missing return statements when a return type
is declared for the method. In Groovy, return statements are optional, there-
fore it’s impossible for the compiler to detect “accidentally” missing returns.

[source,groovy]
----
class ClassWithTypedAndUntypedMethodParams {
    static void main(args) {
        assert 'untyped' == method(1)
        assert 'typed' == method('whatever')
        assert 'two args' == method(1, 2)
    }

    static method(arg) {
        return 'untyped'
    }

    static method(String arg) {
        return 'typed'
    }

    static method(arg1, Number arg2) {
        return 'two args'
    }
}
----



[source,groovy]
.Advanced parameter uses
----
class Summer {
    def sumWithDefaults(a, b, c = 0) {
        a + b + c
    }

    def sumWithList(List args) {
        args.inject(0) { sum, i -> sum += i }
    }

    def sumWithOptionals(a, b, Object[] optionals) {
        return a + b + sumWithList(optionals.toList())
    }

    def sumNamed(Map args) {
        ['a', 'b', 'c'].each { args.get(it, 0) }
        return args.a + args.b + args.c
    }

}

def summer = new Summer()
assert 2 == summer.sumWithDefaults(1, 1)
assert 3 == summer.sumWithDefaults(1, 1, 1)
assert 2 == summer.sumWithList([1, 1])
assert 3 == summer.sumWithList([1, 1, 1])
assert 2 == summer.sumWithOptionals(1, 1)
assert 3 == summer.sumWithOptionals(1, 1, 1)
assert 2 == summer.sumNamed(a: 1, b: 1)
assert 3 == summer.sumNamed(a: 1, b: 1, c: 1)
assert 1 == summer.sumNamed(c: 1)
----

NOTE: There are more ways of implementing parameter lists of variable length.
      You can use varargs with the method(args...) or method(Type[] args) nota-
      tion or even hook into Groovy’s method dispatch by overriding the invoke-
      Method(name, params[]) that every GroovyObject provides.


==== ADVANCED NAMING

[source,groovy]
----
objectReference.methodName()
objectReference.'method Name'()
----

NOTE: Where there’s a string, you can generally also use a GString.
      So how about obj."${var}"() ? Yes, this is also possible, and the GString will
      be resolved to determine the name of the method that’s called on the object!

'''
==== Safe dereferencing with the ?. operator

When a reference doesn’t point to any specific object, its value is null . When calling a
method or accessing a field on a null reference, a NullPointerException ( NPE ) is
thrown. This is useful to protect code from working on undefined preconditions, but
it can easily get in the way of “best-effort” code that should be executed for valid refer-
ences and just be silent otherwise.

[source,groovy]
----
def map = [a: [b: [c: 1]]]
assert map.a.b.c == 1
if (map && map.a && map.a.x) {
    assert map.a.x.c == null
}
try {
    assert map.a.x.c == null
} catch (NullPointerException ignore) {
}
assert map?.a?.x?.c == null
----

==== Constructors

Objects are instantiated from their classes via constructors. If no constructor is given, an
implicit constructor without arguments is supplied by the compiler. This appears to be
exactly like in Java, but because this is Groovy, it should not be surprising that addi-
tional features are available.

===== Positional Parameters
[source,groovy]
----
class VendorWithCtor {
    String name, product

    VendorWithCtor(name, product) {
        this.name = name
        this.product = product
    }
}

def first = new VendorWithCtor('Canoo','ULC')
def second = ['Canoo','ULC'] as VendorWithCtor
VendorWithCtor third = ['Canoo','ULC']
----

===== Named Parameters
[source,groovy]
----
class SimpleVendor {
    String name, product
}

new SimpleVendor()
new SimpleVendor(name: 'Canoo')
new SimpleVendor(product: 'ULC')
new SimpleVendor(name: 'Canoo', product: 'ULC')

def vendor = new SimpleVendor(name: 'Canoo')
assert 'Canoo' == vendor.name


----

===== Implicit Constructors

[source,groovy]
----
java.awt.Dimension area = [200, 100]
assert area.width == 200
assert area.height == 100
----

=== Organizing classes and scripts

Groovy classes are Java classes at the bytecode level, and
consequently, Groovy objects are Java objects in memory. At the source-code level,
Groovy class and object handling is for all practical purposes a superset of the Java syntax.

==== File to class relationship
The relationship between files and class declarations isn’t as fixed as in Java.
Groovy files can contain any number of public class declarations according to the
following rules:

- If a Groovy file contains no class declaration, it’s handled as a script; that is, it’s
transparently wrapped into a class of type Script . This automatically generated
class has the same name as the source script filename 7 (without the extension).
The content of the file is wrapped into a run method, and an additional main
method is constructed for easily starting the script.
- If a Groovy file contains exactly one class declaration with the same name as the file
(without the extension), then there’s the same one-to-one relationship as in Java.
- A Groovy file may contain multiple class declarations of any visibility, and
there’s no enforced rule that any of them must match the filename. The
groovyc compiler happily creates *.class files for all declared classes in such a
file. If you wish to invoke your script directly—for example, using groovy on
the command line or within an IDE —then the first class within your file
should have a main method. 8
- A Groovy file may mix class declarations and scripting code. In this case, the
scripting code will become the main class to be executed, so don’t declare a
class yourself having the same name as the source filename.

[source,groovy]
----
class Vendor {
    public String name
    public String product
    public Address address = new Address()
}

class Address {
    public String street, town, state
    public int zip
}

def canoo = new Vendor()
canoo.name = 'Canoo Engineering AG'
canoo.product = 'UltraLightClient (ULC)'
canoo.address.street = 'Kirschgartenst. 7'
canoo.address.zip = 4051
canoo.address.town = 'Basel'
canoo.address.state = 'Switzerland'
assert canoo.dump() =~ /ULC/
assert canoo.address.dump() =~ /Basel/
----
Vendor and Address are simple data storage classes. They’re roughly equivalent to
struct in C or record in Pascal.

==== Organizing classes in packages

Groovy follows Java’s approach of organizing files in packages of hierarchical struc-
ture. The package structure is used to find the corresponding class files in the filesys-
tem’s directories.

Because *.groovy source files aren’t necessarily compiled to *.class files, there’s
also a need to look up *.groovy files. When doing so, the same strategy is used: the
compiler looks for a Groovy class Vendor in the business package in the file busi-
ness/Vendor.groovy .

===== CLASSPATH
The lookup has to start somewhere, and Java uses its classpath for this purpose. The
classpath is a list of possible starting points for the lookup of *.class files. Groovy
reuses the classpath for looking up *.groovy files.

When looking for a given class, if Groovy finds both a *.class and a *.groovy file, it
uses whichever is newer; that is, it’ll recompile source files into *.class files if they’ve
changed since the previous class file was compiled.

===== PACKAGES

Exactly like in Java, Groovy classes must specify their package before the class defini-
tion. When no package declaration is given, the default package is assumed.
[source,groovy]
----
package business

class Vendor {
    public String name
    public String product
    public Address address = new Address()
}

class Address {
    public String street, town, state
    public int zip
}
----

To reference Vendor in the business package, you can either use business.Vendor
within the code or use import statements for abbreviation.

===== IMPORTS

Groovy follows Java’s notion of allowing import statements before any class
declaration to abbreviate class references.

NOTE: Please keep in mind that unlike in some other scripting languages, an
      import statement has nothing to do with literal inclusion of the imported
      class or file. It merely informs the compiler how to resolve references.

[source,groovy]
----
import business.*
def canoo = new Vendor()
canoo.name = 'Canoo Engineering AG'
canoo.product = 'UltraLightClient (ULC)'
assert canoo.dump() =~ /ULC/
----

.Default import statements
****
By default, Groovy imports six packages and two classes, making it seem like every
Groovy code program contains the following initial statements:
[source,groovy]
----
imoprt java.lang.*
imoprt java.util.*
imoprt java.io.*
imoprt java.net.*
imoprt groovy.lang.*
imoprt groovy.util.*
imoprt java.math.BigInteger
imoprt java.math.BigDecimal
----
****

===== TYPE ALIASING

An import statement has another nice twist: together with the as keyword, it can be
used for type aliasing. Whereas a normal import statement allows a fully qualified class
to be referred to by its base name, a type alias allows a fully qualified class to be referred
to by a name of your choosing. This feature resolves naming conflicts and supports
local changes or bug fixes to a third-party library.
[source,groovy]
----
package thirdparty

class MathLib {
    Integer twice(Integer value) {
        return value * 3
// intentionally wrong!
    }

    Integer half(Integer value) {
        return value / 2
    }
}

package logic

import thirdparty.MathLib as OrigMathLib

class MathLib extends OrigMathLib {
    Integer twice(Integer value) {
        return value * 2
    }
}
// nothing changes below here
def mathlib = new MathLib()
assert 10 == mathlib.twice(5)
assert 2 == mathlib.half(5)
----

[source,groovy]
.avoid names clashes
----
import thirdparty.MathLib as TwiceHalfMathLib
import thirdparty2.MathLib as IncMathLib
def math1 = new TwiceHalfMathLib()
def math2 = new IncMathLib()
assert 3 == math1.half(math2.increment(5))
----

=== Advanced object-oriented features

==== Inheritance

Groovy classes can extend Groovy and Java classes and interfaces alike. Java classes
can also extend Groovy classes and interfaces. You need to compile your Java and
Groovy classes in a particular order for this to work
The only other thing you need to be aware of is that Groovy is more dynamic
than Java when it selects which methods to invoke for you.

==== Using interfaces

In Java, you’d normally write an interface for the plug-in mechanism and then an
implementation class for each plug-in that implements that interface. In Groovy,
dynamic typing allows you to more easily create and use implementations that meet a
certain need. You’re likely to be able to create just two classes as part of developing
two plug-in implementations. In general, you have a lot less scaffolding code and a lot
less typing.

.Implementing interfaces and SAM types
****
If you decide to make heavy use of interfaces, Groovy provides ways to make them
more dynamic. If you have an interface, MyInterface, with a single method and a clo-
sure, myClosure, you can use the as keyword to coerce the closure to be of type
MyInterface. In fact from Groovy 2.2, you don’t even need the as keyword. Groovy
does implicit closure coercion into single abstract method types as shown in this exam-
ple, where the addListener method would normally require an ActionListener:
[source,groovy]
----
import java.awt.event.ActionListener
listeners = []
def addListener(ActionListener al) { listeners << al }
addListener { println "I heard that!" }
listeners*.actionPerformed()
----
Alternatively, if you have an interface with several methods, you can create a map of
closures keyed on the method names and coerce the map to your interface type.
[source,groovy]
----
interface CrudRepository {
    def find(def id)

    def findAll()

    def save(def entity)

    def delete(def id)
}

CrudRepository crudRepository = [
        find   : { "find $it" },
        findAll: { "findAll" },
        save   : { "$it saved"; true },
        delete : { "deleted $it"; true }
] as CrudRepository

assert crudRepository.save("John")
assert crudRepository.delete(7L)
assert crudRepository.find(70L)
assert crudRepository.findAll()
----
****

==== Multimethods
Remember that Groovy’s mechanics of method lookup take the dynamic type of
method arguments into account, whereas Java relies on the static type. This Groovy
feature is called multimethods.
[source,groovy]
----
def oracle(Object o) { return 'object' }

def oracle(String o) { return 'string' }

Object x = 1
Object y = 'foo'
assert 'object' == oracle(x)
assert 'string' == oracle(y)
----
Because Groovy dispatches by the runtime type, the specialized implementation of
oracle(String) is used in the second case.

With this capability in place, you can better avoid duplicated code by being able to
override behavior more selectively. Consider the equals implementation in the fol-
lowing listing that overrides Object ’s default equals method only for the argument
type Equalizer .

[source,groovy]
----
class Equalizer {
boolean equals(Equalizer e){
    return true
    }
}
Object same = new Equalizer()
Object other = new Object()
assert
new Equalizer().equals( same )
assert ! new Equalizer().equals( other )
----

==== Using traits
****
Java’s decision to
adopt single inheritance of implementation greatly simplified the language at the
expense of making it more difficult to support certain kinds of reuse. We’ve all heard
the mantra “prefer delegation over inheritance.” It’s arguable that this is a direct con-
sequence of Java’s restrictions.

A programmer might have the desire to share code
capabilities within their classes without duplication, but given Java’s restrictions, they
create inappropriate subtype relationships. Default methods in Java 8 interfaces lift
this restriction somewhat but still don’t allow a full “design by capability” that includes
state.
****

*Groovy traits* support composition of capabilities. Capabilities that are designed to be
shared are implemented in traits. Your classes can then implement those traits to indi-
cate that they provide that capability. 14 They “inherit” the implementation from the
trait but can override it if they wish. If this sounds like Java 8 default methods, you’re
on the right track, but Groovy traits also support state.

[source,groovy]
----
trait HasId {
    long id
}

trait HasVersion {
    long version
}

trait Persistent {
    boolean save() { println "saving ${this.dump()}" }
}

trait Entity implements Persistent, HasId, HasVersion {
    boolean save() {
        version++
        Persistent.super.save()
    }
}

class Publication implements Entity {  // <1>
    String title
}

class Book extends Publication {
    String isbn
}

Entity gina = new Book(id:1, version:1, title:"gina", isbn:"111111")
gina.save()
assert gina.version == 2
----

At (1) we make Publication an Entity . This is what we call the intrusive way of apply-
ing traits. There’s an even more flexible one: applying them nonintrusively at runtime.
Publications stay totally agnostic of persistency:

[source,groovy]
----
class Publication {
    String title
}

Entity gina = new Book(title:"gina", isbn:"111111") as Entity
gina.id = 1
gina.version = 1

----

NOTE: that gina is no longer of type Book as it was before. That’s the price we pay for
flexibility. But this nonintrusive way of extending a class independent from its inheri-
tance in a type-safe manner is a great way of developing incrementally.

=== Working with GroovyBeans

==== Declaring beans

[source,java]
.java
----
public class MyBean implements java.io.Serializable {
    private String myprop;
    public String getMyprop(){
        return myprop;
    }
    public void setMyprop(String value){
        myprop = value;
    }
}
----

[source,groovy]
.groovy
----
class MyBean implements Serializable {
    String myprop
}
----

[source,groovy]
.Declaring properties in GroovyBeans
----
class MyBean implements Serializable {
    def untyped
    String typed
    def item1, item2
    def assigned = 'default value'
}
def bean = new MyBean()
assert 'default value' == bean.getAssigned()
bean.setUntyped('some value')
assert 'some value' == bean.getUntyped()
bean = new MyBean(typed:'another value')
assert 'another value' == bean.getTyped()
----


.Groovy accessor method to property mappings
|===
|Java |Groovy

|x.getProperty() |x.property
|x.setProperty(y)|x.property=y
|===

[source,groovy]
----
class MrBean {
    String firstname, lastname

    String getName(){
        return "$firstname $lastname"
    }
}

def bean = new MrBean(firstname: 'Rowan')
bean.lastname = 'Atkinson'

assert 'Rowan Atkinson' == bean.name
----

[source,groovy]
.Advanced accessors with Groovy
----
class DoublerBean {
    public value

    void setValue(value){
        this.value=value
    }

    def getValue(){
        value * 2
    }
}

def bean = new DoublerBean(value: 100)
assert 200 == bean.value
assert 100 == bean.@value
----

IMPORTANT: Inside the lexical scope of a field, references to fieldname or
           this.fieldname are resolved as field access, not as property access. The same
           effect can be achieved from outside the scope using the reference.@field-
           name syntax.

===== BEAN-STYLE EVENT HANDLING

Groovy supports event listeners in a simple but powerful way.

[source,java]
.java
----
// Java
final JButton button = new JButton("Push me!");
button.addActionListener(new IActionListener(){
    public void actionPerformed(ActionEvent event){
        System.out.println(button.getText());
    }
});
----

A Groovy programmer only has to attach a closure to the button as if it were a field
named by the respective callback method:

[source,groovy]
----
button = new JButton('Push me!')
button.actionPerformed = { event ->
    println button.text
}
----
NOTE: Groovy uses bean introspection to determine whether a field setter refers to
      a callback method of a listener that’s supported by the bean. If so, a Closure-
      Listener is transparently added that calls the closure when notified. A
      ClosureListener is a proxy implementation of the required listener interface.

==== Using bean methods for any object

[source,groovy]
----
class ClassWithProperties {
    def someProperty
    public someField
    private somePrivateField
}

def obj = new ClassWithProperties()
def store = []
obj.properties.each { property ->
    store += property.key
    store += property.value
}
assert store.contains('someProperty')
assert store.contains('someField') == false
assert store.contains('somePrivateField') == false
assert store.contains('class')
assert obj.properties.size() == 2
----



==== Fields, accessors, maps, and Expando

`object.name` Here’s what happens when Groovy resolves this reference:

- If object refers to a map, object.name refers to the value corresponding to the
name key that’s stored in the map. Otherwise, if name is a property of object ,
the property is referenced .
- Every Groovy object has the opportunity to implement its own getProperty
(name) and setProperty(name, value) methods. When it does, these imple-
mentations are used to control the property access. Maps, for example, use this
mechanism to expose keys as properties.
- Field access can be intercepted by providing the object.get(name) method, This is a last resort as far as the Groovy runtime is con-
cerned: it’s used only when there’s no appropriate JavaBeans property available
and when getProperty isn’t implemented.


===== Expando
An Expando can be thought of as an expandable alternative to a bean.

[source,groovy]
----
def boxer = new Expando()
assert null == boxer.takeThis
boxer.takeThis = 'ouch!'
assert 'ouch!' == boxer.takeThis
boxer.fightBack = {times -> delegate.takeThis * times}
assert 'ouch!ouch!ouch!' == boxer.fightBack(3)
----

==== Using advanced syntax features

[source,groovy]
----
this.class.methods

this.class.methods.name

this.class.methods.name.grep(~/get.*/).sort()

list.property == list.collect{ item -> item?.property }
list*.member == list.collect{ item -> item?.member }
----

[source,groovy]
.examples using Gpath
----
class Invoice {
    List  items
    Date date
}

class LineItem {
    Product product
    int count

    int total() {
        return product.dollar * count
    }
}

class Product {
    String name
    def dollar
}

def ulcDate =Date.parse('yyyy-MM-dd', '2015-01-01')
def otherDate= Date.parse('yyyy-MM-dd', '2015-02-02')
def ulc = new Product(dollar:1499, name:'ULC')
def ve = new Product(dollar:499, name:'Visual Editor')


def invoices = [
        new Invoice(date:ulcDate, items: [
        new LineItem(count:5, product:ulc),
        new LineItem(count:1, product:ve)
    ]),
        new Invoice(date:otherDate, items: [
        new LineItem(count:4, product:ve)
    ])
]

def allItems = invoices.items.flatten()

assert [5*1499, 499, 4*499] == allItems*.total()

assert ['ULC'] == allItems.grep{it.total() > 7000}.product.name

def searchDates = invoices.grep{
    it.items.any{it.product == ulc}
}.date*.toString()

assert [ulcDate.toString()] == searchDates
----

==== Injecting the spread operator

Groovy provides a * (spread) operator that’s connected to the spread-dot operator in
that it deals with tearing a list apart. It can be seen as the reverse counterpart of the
subscript operator that creates a list from a sequence of comma-separated objects.
The spread operator distributes all items of a list to a receiver that can take this
sequence. Such a receiver can be a method that takes a sequence of arguments or a
list constructor.

[source,groovy]
----
def getList() {
    return [1, 2, 3]
}

def sum(a, b, c) {
    return a + b + c
}

assert 6 == sum(*list)


def range = (1..3)
assert [0,1,2,3] == [0,*range]

def map = [a:1,b:2]
assert [a:1, b:2, c:3] == [c:3, *:map]
----

==== Concise syntax with command chains

Command chains are such a feature that’s based on a very simple idea: one can
omit dots and parentheses in chain-of-method calls.

[source,groovy]
----
link(producer).to(consumer)
link producer to consumer

move(10, forward).painting(color:blue)
move 10, forward painting color:blue

----

IMPORTANT: It works with all methods that have at least one argument.