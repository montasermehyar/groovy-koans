[[dynamic-programming]]

== Dynamic Programming
<<<

=== What is dynamic programming?

In classic object-oriented systems, every class has a well-known set of states, captured in
the fields of that class, and well-known behavior, defined by its methods. Neither the
set of states nor the behavior ever changes after compilation, and it’s identical for all
instances of a class.

.Dynamic programming
****
breaks this limitation by allowing the introduction of a
new state, or even more importantly, allowing the addition of a new behavior or modi-
fication of an existing one.
****


.What is “meta”?
----
Meta means applying a concept onto itself—for example, metainformation is information about information.
Likewise, because programming is “writing code,” metaprogramming means writing code that writes code.
----

=== Meta Object Protocol

[source,groovy]
----
println 'Hello' // Groovy
InvokerHelper.invokeMethod(this, "println", {"Hello"}); // Java
----

NOTE: Every innocent method call that you write in Groovy is really a call into
      the MOP , regardless of whether the call target has been compiled with Groovy
      or Java. This applies equally to static and instance method calls, constructor
      calls, and property access, even if the target is the same object as the caller.


==== Customizing the MOP with hook methods

===== MethodMissing

[source,groovy]
----
Object methodMissing(String name, Object arguments)

class Pretender {
    def methodMissing(String name, Object args) {
        "called $name with $args"
    }
}
def bounce = new Pretender()
assert bounce.hello('world') == 'called hello with [world]'
----

[source,groovy]
.Using methodMissing to simulate a miniature GORM
----
class MiniGorm {
def db = []
    def methodMissing(String name, Object args) {
        db.find { it[name.toLowerCase()-'findby'] == args[0] }
    }
}

def people=new MiniGorm()
def dierk=[first:'Dierk',last:'Agerm']
def paul=[first:'Paul',last:'King']
assert people.findByFirst('Dierk') == dierk
assert people.findByLast('King') == paul
----



===== PropertyMissing

[source,groovy]
----
Object propertyMissing(String name)

class PropPretender {
    def propertyMissing(String name) {
        "accessed $name"
    }
}

def bounce = new PropPretender()
assert bounce.hello == 'accessed hello'
----

[source,groovy]
----
def propertyMissing(String name) {
    int result = 0
    name.each {
        result <<= 1
        if (it == 'I') result++
    }
    return result
}
IIOI + IOI == IOOIO
----


===== Using closures for dynamic hooks

[source,groovy]
----
class DynamicPretender {
    Closure whatToDo = { name -> "accessed $name"}
    def propertyMissing(String name) {
    whatToDo(name)
    }
}

def one = new DynamicPretender()
assert one.hello == 'accessed hello'
one.whatToDo = { name -> name.size() }
assert one.hello == 5
----

==== Customizing GroovyObject methods

[source,groovy]
----
public interface GroovyObject {
    Object invokeMethod(String methodName, Object args);
    Object getProperty(String propertyName);
    void setProperty(String propertyName, Object newValue);
    MetaClass getMetaClass();
    void setMetaClass(MetaClass metaClass);
}

public abstract class GroovyObjectSupport implements GroovyObject {
    public Object invokeMethod(String name, Object args) {
        return getMetaClass().invokeMethod(this, name, args);
    }
    public Object getProperty(String property) {
        return getMetaClass().getProperty(this, property);
    }
    public void setProperty(String property, Object newValue) {
        getMetaClass().setProperty(this, property, newValue);
    }
// more here...
}
----
NOTE: You can fool the MOP into thinking that a class that was actually com-
      piled by Java was compiled by Groovy. You only need to implement the Groovy-
      Object interface or, more conveniently, extend GroovyObjectSupport .

As soon as a class implements GroovyObject , the following rules apply:

- Every access to a property calls the `getProperty()` method.
- Every modification of a property calls the `setProperty()` method.
- Every call to an unknown method calls `invokeMethod()` . If the method is known,
`invokeMethod()` is only called if the class implements `GroovyObject` and the
marker interface `GroovyInterceptable`.

[source,groovy]
.Using getProperty to call parameterless methods without parentheses
----
class NoParens {
    def getProperty(String propertyName) {
        if (metaClass.hasProperty(this, propertyName)) {
            return metaClass.getProperty(this, propertyName)
        }
        invokeMethod propertyName, null
    }
}

class PropUser extends NoParens {
    boolean existingProperty = true
}
def user = new PropUser()
assert user.existingProperty
assert user.toString() == user.toString
----


NOTE: Once you’ve implemented getProperty() , every property will be
      found and thus propertyMissing() will no longer be called.

=== Modifying behavior through the metaclass

==== MetaClass knows it all

[source,groovy]
----

MetaClass mc = String.metaClass
final Object[] NO_ARGS = []
assert 1 == mc.respondsTo("toString", NO_ARGS).size()
assert 3 == mc.properties.size()
assert 76 == mc.methods.size()
assert 177 == mc.metaMethods.size()
assert "" == mc.invokeMethod("","toString", NO_ARGS)
assert null == mc.invokeStaticMethod(String, "println", NO_ARGS)
assert "" == mc.invokeConstructor(NO_ARGS)
----

.Calling a method means calling the metaclass
****
You can assume that Groovy never calls methods directly in the bytecode but always
through the object’s metaclass. At least, this is how it looks to you as a programmer.

Behind the scenes there are optimizations going on that technically circumvent the
metaclass, but only when it’s safe to do so.
****

==== How to find the metaclass and invoke methods

Objects that don’t inherit from GroovyObject aren’t asked for the metaClass
property. Their metaclass is retrieved from the MetaClassRegistry .

IMPORTANT: The default metaclass can be changed from the outside with-
           out touching any application code. Let’s assume you have a class Custom in
           package custom . Then you can change its default metaclass by putting a meta-
           class with the name groovy.runtime.metaclass.custom.CustomMetaClass on
           the classpath. This device has been proven useful when inspecting large Groovy
           codebases in production.

[source,groovy]
----
// MOP pseudo code
def mopInvoke(Object obj, String method, Object args) {
    if (obj instanceof GroovyObject) {
        return groovyObjectInvoke(obj, method, args)
    }
    registry.getMetaClass(obj.class).invokeMethod(obj, method, args)
}
----

[source,groovy]
----
def groovyObjectInvoke(Object obj, String method, Object args) {

    if (obj instanceof GroovyInterceptable) {
        return obj.metaClass.invokeMethod(method, args)
    }
    if (!obj.metaClass.respondsTo(method, args))
        return obj.metaClass.invokeMethod(method, args)

    obj.metaClass.invokeMethod(obj, method, args)
}
----

[source,groovy]
----
// Default meta class pseudo code
def invokeMethod(Object obj, String method, Object args) {
    if (obj.metaClass.respondsTo(method, args)) {
        return methodCall(obj, method, args)
    }
    if (methodMissingAvailable(obj)) {
        return obj.metaClass.methodMissing(method, args)
    }
    throw new MissingMethodException()
}
----

==== Setting other metaclasses

Groovy comes with a number of metaclasses:

- The default metaclass MetaClassImpl , which is used in the vast majority of cases
- The ExpandoMetaClass , which can expand the state and behavior
- A ProxyMetaClass , which can decorate a metaclass with interception capabilities
- Additional metaclasses that are used internally and for testing purposes

[source,groovy]
.Assigning a ProxyMetaClass to a GroovyObject for tracing method calls
----

class InspectMe {
int outer(){
return inner()
}
private int inner(){
return 1
}
}
def tracer = new TracingInterceptor(writer: new StringWriter())
def proxyMetaClass = ProxyMetaClass.getInstance(InspectMe)
proxyMetaClass.interceptor = tracer
InspectMe inspectMe = new InspectMe()
inspectMe.metaClass = proxyMetaClass

assert 1 == inspectMe.outer()
assert "\n" + tracer.writer.toString() == """
before InspectMe.outer()
before InspectMe.inner()
after InspectMe.inner()
after InspectMe.outer()
"""
----


.Interceptors are more than aspects
****
Interceptors may remind one or the other reader of aspect-oriented programming
(AOP) and the TracingInterceptor suggests this connotation. But interceptors can
do much more: they can redirect to a different method, change the arguments, sup-
press the method call, and even change the return value!
****

[source,groovy]
----
def boxer = new Expando()
boxer.takeThis = 'ouch!'
boxer.fightBack = { times -> takeThis * times}
assert boxer.fightBack(3) == 'ouch!ouch!ouch!'
----

[source,groovy]
.Adding low() to java.lang.String via ExpandoMetaClass
----
assert String.metaClass =~ /MetaClassImpl/
String.metaClass.low = {-> delegate.toLowerCase() }
assert String.metaClass =~ /ExpandoMetaClass/
assert "DiErK".low() == "dierk"
----

[source,groovy]
.Modifying the metaclass of a class (Groovy and Java)
----
class MyGroovy1 { }
def before = new MyGroovy1()
MyGroovy1.metaClass.myProp = "MyGroovy prop"
MyGroovy1.metaClass.test = {-> myProp }

try {
before.test()
assert false, "should throw MME"
} catch(mme) { }

assert new MyGroovy1().test() == "MyGroovy prop"
----

[source,groovy]
.Modifying the metaclass of a Groovy instance
----
class MyGroovy2 {}

def myGroovy = new MyGroovy2()
myGroovy.metaClass.myProp = "MyGroovy prop"
myGroovy.metaClass.test = { -> myProp }
try {
    new MyGroovy2().test()
    assert false, "should throw MME"
} catch (mme) {
}
----


[source,groovy]
.Modifying the metaclass of a Java instance
----
def myJava = new String()
myJava.metaClass.myProp = "MyJava prop"
myJava.metaClass.test = { -> myProp }
try {
    new String().test()
    assert false, "should throw MME"
} catch (mme) {
}

assert myJava.test() == "MyJava prop"
----

[source,groovy]
.Decoding A Space Odyssey with a metaclass builder
----
def move(string, distance) {
    string.collect { (it as char) + distance as char }.join()
}
String.metaClass {
    shift = -1
    encode {-> move delegate, shift }
    decode {-> move delegate, -shift }
    getCode {-> encode() }
    getOrig {-> decode() }
}
assert "IBM".encode() == "HAL"
assert "HAL".orig == "IBM"
def ibm = "IBM"
ibm.shift = 7
assert ibm.code == "PIT"
----


NOTE: Modifying the metaclass of the String class will affect all future String
      instances.

[source,groovy]
.Adding a static method to a class
----
Integer.metaClass.static.answer = {-> 42}
assert Integer.answer() == 42
----

[source,groovy]
.Metaclass changes for superclasses and interfaces
----
class MySuperGroovy { }
class MySubGroovy extends MySuperGroovy { }
MySuperGroovy.metaClass.added = {-> true }
assert new MySubGroovy().added()
Map.metaClass.toTable = {->
    delegate.collect{ [it.key, it.value] }
}
assert [a:1, b:2].toTable() == [
        ['a', 1],
        ['b', 2]
]
----

[source,groovy]
.Metaclass injection of operator and MOP hook methods
----
String.metaClass {
    rightShiftUnsigned = { prefix ->
        delegate.replaceAll(~/\w+/) { prefix + it }
    }
    methodMissing = { String name, args->
        delegate.replaceAll name, args[0]
    }
}

def people = "Dierk,Guillaume,Paul,Hamlet,Jon"
people >>>= "\n"
people = people.Dierk('Mittie').Guillaume('Mr.G')
assert people == '''
Mittie,
Mr.G,
Paul,
Hamlet,
Jon'''
----

Some takeaways and rules of thumb for metaclasses:

- All method calls from Groovy code go through a metaclass.
- Metaclasses can change for all instances of a class or per a single instance.
- Metaclass changes affect all future instances in all running threads.
- Metaclasses allow nonintrusive changes to both Groovy and Java code as
long as the caller is Groovy. We can even change access to final classes like
`java.lang.String` .
- Metaclass changes can take the form of property accessors (pretending prop-
erty access), operator methods, GroovyObject methods, or MOP hook methods.
- ExpandoMetaClass makes metaclass modifications more convenient.
- Metaclass changes are best applied only once, preferably at application
startup time.

===== Temporary MOP modifications using category classes

The last point directly leads us to another concept of dynamic programming in Groovy.
ExpandoMetaClass isn’t designed for easily removing a once dynamically added
method or undoing any other change. For such temporary changes, Groovy provides
category classes.

Using a category class is trivial. Groovy adds a use method to java.lang.Object
that takes two parameters: a category class (or any number thereof) and a closure:

[source,groovy]
----
use CategoryClass, {
    // new methods are available
}
// new methods are no longer available
----

[source,groovy]
.How to use existing categories like TimeCategory and Collections
----
import groovy.time.TimeCategory

def janFirst1970 = new Date(0)
use TimeCategory, {
    Date
    xmas = janFirst1970 + 1.year - 7.days
    assert xmas.month == Calendar.DECEMBER
    assert xmas.date == 25
}
use Collections, {
    def list = [0, 1, 2, 3]
    list.rotate 1
    assert list == [3, 0, 1, 2]
}
----

Category classes are by no means special. Neither do they implement a certain
interface nor do they inherit from a certain class. They aren’t configured or registered
anywhere! They just happen to contain static methods with at least one parameter.

When a class is used as an argument to the use method, it becomes a category class
and every static method like
[source,groovy]
----
static ReturnType methodName(Receiver self, optionalArgs) {...}
----
becomes available on the receiver as if the Receiver had an instance method like
[source,groovy]
----
ReturnType methodName(optionalArgs) {...}
----

[source,groovy]
.Running a category to marshal and unMarshal integers to/from strings
----
class Marshal {
    static String marshal(Integer self) {
        self.toString()
    }
    static Integer unMarshal(String self) {
        self.toInteger()
    }
}

use Marshal, {
    assert 1.marshal() == "1"
    assert "1".unMarshal() == 1
    [Integer.MIN_VALUE, -1, 0, Integer.MAX_VALUE].each {
        assert it.marshal().unMarshal() == it
    }
}
----

Naming the receiver object self is just a convention. You can use any name you want.
Groovy’s design decision of using static methods to implement category behavior has
a few beneficial effects.

- You’re much less likely to run into concurrency issues, because there’s less
shared state.
- You can use a plethora of classes as categories even if they’ve been implemented
without knowing about Groovy. Collections was just an example of many
classes with static methods that reside in widely used helper libraries.
- They can easily be created in Groovy, Java, or any other JVM language that pro-
duces classes and static methods.

Category classes are a good place to collect methods that work conjointly on different
types, such as Integer and String , to accomplish a feature like marshaling.
Key characteristics of using category classes are:

- The use method applies categories to the runtime scope of the closure (as
opposed to the lexical scope). That means you can extract code from the clo-
sure into a method and call the method from inside the closure.
- Category use is confined to the current thread.
- Category use is nonintrusive.
- If the receiver type refers to a superclass or even an interface, then the method
will be available in all subclasses/implementors without further configuration.
- Category method names can well take the form of property accessors (pretend-
ing property access), operator methods, and GroovyObject methods. MOP
hook methods cannot be added through a category class. 10
- Category methods can override method definitions in the metaclass.
- Where performance is crucial, use categories with care and measure their
influence.
- Categories cannot introduce a new state in the receiver object; they cannot add
new properties with a backing field.


===== Writing extension modules

Extension modules can be seen as categories that are always visible: you don’t need to
call use to enable the methods. Just like Groovy enriches the JDK classes with custom
methods, you can make your categories globally visible and make them behave like
methods from the GDK .

One of the most interesting use cases for this is that you can
bundle such extension modules into their own JAR file and make them available to
other programs just by adding the JAR file to your classpath.

Converting a category into an extension module is straightforward. Imagine that
you want to use the Marshal category defined in previous listing  without having to explicitly use the category.
To achieve that, you only need two steps:

- Write the Marshal class into its own source file .
- Write an extension module descriptor and make it available on a classpath.

You need to create a
file named org.codehaus.groovy.runtime.ExtensionModule and ensure it’s found
in the META-INF/services folder of your JAR . This file is used internally by Groovy to load your extension module and make the category transparently available.
The descriptor file consists of four entries:
[source]
.org.codehaus.groovy.runtime.ExtensionModule
----
moduleName=regina-marshal
moduleVersion=1.0
extensionClasses=regina.Marshal
staticExtensionClasses=
----

The moduleName and moduleVersion entries are used by Groovy when the runtime is
initialized. If two versions of a module of the same name are found on the classpath,
the module will not be loaded and an error will be thrown. The extensionClasses
entry is a comma-separated list of category-like classes. This means that you can define
multiple categories in a single extension module.

[source]
.StringUtils
----
moduleName=apache-commons-stringutils
moduleVersion=3.2
extensionClasses=org.apache.commons.lang3.StringUtils
----



===== Using the @Category annotation

With @Category , you write your class as if it were an instance class but the annotation
adjusts it to have the required format needed for categories, meaning, methods are
made static and the self parameter:

[source,groovy]
----
@Category(Integer)
class IntegerMarshal {
    String marshal() {
        toString()
    }
}
@Category(String)
class StringMarshal {
    Integer unMarshal() {
        this.toInteger()
    }
}

use ([IntegerMarshal, StringMarshal]) {
    assert1.marshal() == "1"
    assert "1".unMarshal() == 1
}
----

The @Category annotation can only be used for creating categories associated with a single class; therefore we split our category into two.

===== Merging classes with Mixins

If you have a superclass A with a subclass B then any object of class B isn’t only a B , it
also is an A ! The definitions of A and B typically reside in different files. 12 The situation
looks as if A and B would be merged when constructing an instance of B .
They share both state and behavior.
This class merging by inheritance is pretty restricted in Java.

- You cannot use it when inheritance has already been used for other purposes.
- You cannot merge (inherit from) more than one class.
- It’s intrusive. You have to change the class definition.
- You cannot do it with final classes.

[source,groovy]
----
@Mixin(MessageFeature)
class FirstTest extends GroovyTestCase {
    void testWithMixinUsage() {
        message = "Called from Test"
        assertMessage "Called from Test"
    }
}

class MessageFeature {
    def message
    void assertMessage(String msg) {
        assertEquals msg, message
    }
}
----


[source,groovy]
.Mixing in multiple sieve features nonintrusively
----
class EvenSieve {
    def getNo2() {
        removeAll { it % 2 == 0}
        return this
    }
}
class MinusSieve {
    def minus(int num) {
        removeAll { it % num == 0}
        return this
    }
}
ArrayList.mixin EvenSieve, MinusSieve
assert (0..10).toList().no2 - 3 - 5 == [1, 7]
----

Mixins are often compared with multiple inheritance but they’re of a different nature.

In the first place, our ArrayList doesn’t become a subtype of MinusSieve .

Any instanceof test will fail. There’s no is-a relationship and no polymorphism.
You can use enforced type coercion with the as operator, though.

Unlike many models of multiple inheritance, the mixing in of new features always
happens in traceable sequence and, in case of conflicts, the latest addition wins.
Mixins work like metaclass changes in that respect.

Mixins are designed for sharing features while not modifying any existing
behavior of the receiver.
Features can build on top of each other and merge and blend with the receiver

.MOP priorities
****
It’s always good advice to keep things simple. With dynamic programming one can
easily go overboard by doing too much, such as using category classes, metaclass
changes, and Mixins in combination. If you do anyway, then categories are looked at
first, then the metaclass, and finally the Mixins: +

`category class > meta class > mixin` +

But this only applies to methods that are defined for the same class and have the
same parameter types. Otherwise, the rules for method dispatch by class/super-
class/interface take precedence.
****

NOTE: In case of multiple method definitions, a category class shadows a pre-
      viously applied category class. Changes to an ExpandoMetaClass override
      previously added methods in that metaclass. Later applied Mixins shadow
      previously applied Mixins.

=== Real-world dynamic programming in action

[source,groovy]
.Replacing constructors with factory methods
----
import java.awt.Dimension

Class.metaClass.make = { Object[] args ->
    delegate.metaClass.invokeConstructor(*args)
}

assert new HashMap() == HashMap.make()
assert new Integer(42) == Integer.make(42)
assert new Dimension(2, 3) == Dimension.make(2, 3)
----

[source,groovy]
.Fooling IDEs for fun and profit
----
interface ChannelComponent {}
class Producer implements ChannelComponent {
    List<Integer> outChannel
}
class Adaptor implements ChannelComponent {
    List<Integer> inChannel
    List<String> outChannel
}
class Printer implements ChannelComponent {
    List<String> inChannel
}
class WiringCategory {
    static connections = []
    static setInChannel(ChannelComponent self, value){
        connections << [target:self, source:value]
    }
    static getOutChannel(ChannelComponent self){
        self
    }
}

Producer producer = new Producer()
Adaptor adaptor = new Adaptor()
Printer printer = new Printer()
use WiringCategory, {
    adaptor.inChannel = producer.outChannel
    printer.inChannel = adaptor.outChannel
}

assert WiringCategory.connections == [
        [source: producer, target: adaptor],
        [source: adaptor, target: printer]
]
----
[source,groovy]
.Method aliasing and undoing metaclass modifications
----
MetaClass oldMetaClass = String.metaClass
MetaMethod alias = String.metaClass.metaMethods
        .find { it.name == 'size' }
String.metaClass {
    oldSize = { -> alias.invoke delegate }
    size = { -> oldSize() * 2 }
}
assert "abc".size() == 6
assert "abc".oldSize() == 3

if (oldMetaClass.is(String.metaClass)){
    String.metaClass {
        size = { -> alias.invoke delegate }
        oldSize = { -> throw new UnsupportedOperationException() }
    }
}else {
    String.metaClass = oldMetaClass
}

assert "abc".size() == 3
----
[source,groovy]
.The Intercept/Cache/Invoke pattern
----
ArrayList.metaClass.methodMissing = { String name, Object args ->
    assert name.startsWith("findBy")
    assert args.size() == 1
    Object.metaClass."$name" = { value ->
        delegate.find { it[name.toLowerCase() - 'findby'] == value }
    }
    delegate."$name"(args[0])
}

def data = [
        [name: 'moon', au  : 0.0025],
        [name: 'sun', au  : 1],
        [name: 'neptune', au: 30]
]
assert data.findByName('moon')
assert data.findByName('sun')
assert data.findByAu(1)
----

compile-time_metaprogramming_and_ast_transformations